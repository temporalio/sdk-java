description = '''Spring Boot AutoConfigure for Temporal Java SDK'''

ext {
    otelVersion = '1.25.0'
    otShimVersion = "${otelVersion}-alpha"
}

if (project.hasProperty("springBoot4Test")) {
    // SB4 requires Java 17+. Override --release 8 (from gradle/java.gradle) for test
    // compilation only; main sources stay compiled against SB 2.7 at Java 8 target.
    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    compileTestJava {
        options.compilerArgs.removeAll(['--release', '8'])
        options.compilerArgs.addAll(['--release', '17'])
    }
}

dependencies {
    api(platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))
    api(platform("io.opentelemetry:opentelemetry-bom:$otelVersion"))

    compileOnly project(':temporal-sdk')
    compileOnly project(':temporal-testing')

    api "org.springframework.boot:spring-boot-autoconfigure"

    api project(':temporal-opentracing')
    implementation "io.opentelemetry:opentelemetry-api"
    implementation "io.opentelemetry:opentelemetry-opentracing-shim:$otShimVersion"

    if (JavaVersion.current().isJava9Compatible()) {
        //needed for the generated grpc stubs and is not a part of JDK since java 9
        compileOnly "javax.annotation:javax.annotation-api:$annotationApiVersion"
    }

    // this dependency is not mandatory for the module to work, but it provides a better IDE experience developing the module
    // it's needed to auto-generate configuration metadata. See for more details:
    // https://docs.spring.io/spring-boot/docs/2.4.0/reference/html/appendix-configuration-metadata.html#configuration-metadata-annotation-processor
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"

    testImplementation project(':temporal-testing')

    testImplementation "org.mockito:mockito-core:${mockitoVersion}"

    testImplementation "org.springframework.boot:spring-boot-starter-test"

    if (project.hasProperty("springBoot4Test")) {
        // Override the SB 2.7 BOM for test classpath so tests run against SB 4.
        // Main classes are still compiled against SB 2.7 â€” we're validating that
        // the published JAR works at runtime with SB 4.
        testImplementation(platform("org.springframework.boot:spring-boot-dependencies:$springBoot4Version"))
        // SB4 requires SLF4J 2.x + Logback 1.5+; let the SB4 BOM provide them
        // instead of using the root project's pinned versions.
        testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    } else {
        testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: "${logbackVersion}"
        testImplementation('org.slf4j:slf4j-api') {
            version {
                strictly "${slf4jVersion}"
            }
        }
    }
}

tasks.test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}
