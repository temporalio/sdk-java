buildscript {
    dependencies {
        classpath "gradle.plugin.org.jruyi.gradle:thrift-gradle-plugin:0.4.0"
    }
}

plugins {
    id 'net.minecrell.licenser' version '0.3'
    id "com.github.sherter.google-java-format" version "0.6"
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "org.jruyi.thrift"
apply plugin: 'maven-publish'
apply plugin: 'com.github.sherter.google-java-format'

googleJavaFormat {
    toolVersion '1.3'
    include '**/*.java'
    exclude '**/generated-sources/*'
}

group = 'com.uber'
version = '0.2.0-SNAPSHOT'

description = """Uber Cadence Java Client"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile group: 'com.uber.tchannel', name: 'tchannel-core', version: '0.7.6'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile(group: 'org.apache.thrift', name: 'libthrift', version: '0.9.3')
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

license {
    header rootProject.file('license-header.txt')
    exclude 'com/uber/cadence/*.java' // generated code
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

license {
    header = project.file('license-header.txt')
    exclude '**/com/uber/cadence/*.java'
}


compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
    repositories {
        mavenLocal()
    }
}
