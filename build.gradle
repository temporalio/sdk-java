buildscript {
    ext {
        palantirGitVersionVersion = "${JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11) ? '0.15.0' : '0.13.0'}"
    }
}

plugins {
    id 'net.ltgt.errorprone' version '2.0.2' apply false
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.palantir.git-version' version "${palantirGitVersionVersion}" apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'com.diffplug.spotless' version '6.8.0' apply false
    id 'base'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

ext {
    // Platforms
    grpcVersion = '1.47.0' // [1.34.0,)
    jacksonVersion = '2.13.1' // [2.9.0,)
    micrometerVersion = '1.9.2' // [1.0.0,)
    springBootVersion = '2.7.2'// [2.4.0,)

    slf4jVersion = '1.7.36' // [1.4.0,)
    protoVersion = '3.20.1' // [3.10.0,)
    annotationApiVersion = '1.3.2'
    guavaVersion = '31.1-jre' // [10.0,)
    tallyVersion = '0.11.1' // [0.4.0,)

    gsonVersion = '2.9.0' // [2.0,)

    jsonPathVersion = '2.7.0' // compileOnly

    // test scoped
    logbackVersion = '1.2.11'
    mockitoVersion = '4.6.1'
    junitVersion = '4.13.2'
}

apply from: "$rootDir/gradle/versioning.gradle"
apply from: "$rootDir/gradle/java.gradle"
if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11)) {
    apply from: "$rootDir/gradle/linting.gradle"
}
apply from: "$rootDir/gradle/errorprone.gradle"
apply from: "$rootDir/gradle/licensing.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/dependencyManagement.gradle"
if (project.project.hasProperty("jacoco")) {
    apply from: "$rootDir/gradle/jacoco.gradle"
}