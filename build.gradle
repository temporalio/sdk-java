buildscript {
    ext {
        palantirGitVersionVersion = "${JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11) ? '0.15.0' : '0.13.0'}"
        kotlinVersion = "${JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_16) ? '1.5.32' : '1.4.32'}"
    }
}

plugins {
    id 'net.ltgt.errorprone' version '3.0.1' apply false
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.palantir.git-version' version "${palantirGitVersionVersion}" apply false
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'com.diffplug.spotless' version '6.11.0' apply false
    id 'com.github.nbaztec.coveralls-jacoco' version "1.2.15" apply false

    //    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    //    id 'org.jetbrains.kotlin.jvm' version '1.5.32'
    //    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    //    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}" apply false
    id 'base'
}

allprojects {
    repositories {
        mavenCentral()
    }
}

ext {
    // Platforms
    grpcVersion = '1.50.0' // [1.34.0,)
    jacksonVersion = '2.13.4.20221012' // [2.9.0,)
    micrometerVersion = '1.9.4' // [1.0.0,)

    slf4jVersion = '1.7.36' // [1.4.0,) // stay on 1.x for a while to don't use any APIs from 2.x which may break our users which decide on 1.x
    protoVersion = '3.21.6' // [3.10.0,)
    annotationApiVersion = '1.3.2'
    guavaVersion = '31.1-jre' // [10.0,)
    tallyVersion = '0.11.1' // [0.4.0,)

    gsonVersion = '2.9.1' // [2.0,)

    jsonPathVersion = '2.7.0' // compileOnly

    springBootVersion = '2.7.4'// [2.4.0,)

    // test scoped
    logbackVersion = '1.2.11'
    mockitoVersion = '4.8.1'
    junitVersion = '4.13.2'
}

apply from: "$rootDir/gradle/versioning.gradle"
apply from: "$rootDir/gradle/java.gradle"
if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_11)) {
    apply from: "$rootDir/gradle/linting.gradle"
}
apply from: "$rootDir/gradle/errorprone.gradle"
apply from: "$rootDir/gradle/licensing.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/dependencyManagement.gradle"
if (project.hasProperty("jacoco")) {
    apply from: "$rootDir/gradle/jacoco.gradle"
}