nexusPublishing {
    // to release to sonatype use ./gradlew publishToSonatype
    repositories {
        sonatype {
            username = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : ''
            password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : ''
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                afterEvaluate {
                    plugins.withId('java-platform') {
                        from components.javaPlatform
                    }
                    plugins.withId('java') {
                        from components.java
                    }
                }
            }
        }

        // To test local publishing use ./gradlew publish and comment nexusPublishing
        //
        // repositories {
        //     maven {
        //         def releasesRepoUrl = "$System.env.HOME/repos/releases"
        //         def snapshotsRepoUrl = "$System.env.HOME/repos/snapshots"
        //         url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        //     }
        // }
    }

    // Customize the pom for all publications and sign all, in afterEvaluate so
    // subproject build.gradle files can contribute (e.g. description) and custom
    // publications (e.g. shadow) exist.
    afterEvaluate { subproject ->
        subproject.publishing.publications.withType(MavenPublication).each { publication ->
            publication.pom {
                name = subproject.description
                description = subproject.description
                url = 'https://github.com/temporalio/sdk-java'

                scm {
                    connection = 'scm:git@github.com:temporalio/sdk-java.git'
                    developerConnection = 'scm:git@github.com:temporalio/sdk-java.git'
                    url = 'https://github.com/temporalio/sdk-java.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'mfateev'
                        name = 'Maxim Fateev'
                        email = 'maxim@temporal.io'
                    }
                    developer {
                        id = 'samarabbas'
                        name = 'Samar Abbas'
                        email = 'samar@temporal.io'
                    }
                }
            }
        }
        if (subproject.hasProperty('signing.keyId')) {
            subproject.signing {
                subproject.publishing.publications.withType(MavenPublication).each { publication ->
                    sign publication
                }
            }
        }
    }
}
